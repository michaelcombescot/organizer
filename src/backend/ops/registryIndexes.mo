import Principal "mo:core/Principal";
import Blob "mo:core/Blob";
import Map "mo:core/Map";
import Iter "mo:core/Iter";
import List "mo:core/List";
import Timer "mo:core/Timer";
import CanistersKinds "../shared/canistersKinds";
import MixinOpsOperations "../canisters/mixins/mixinOpsOperations";

// only goal of this canister is too keep track of all the indexes and serve their principal to the frontend.
// not dynamically created, if the need arise another instance will need to be declared in the dfx.json
shared ({ caller = owner }) persistent actor class RegistryIndexes() = this {
    include MixinOpsOperations({
        coordinatorPrincipal    = owner;
        canisterPrincipal       = Principal.fromActor(this);
        toppingThreshold        = 2_000_000_000_000;
        toppingAmount           = 2_000_000_000_000;
        toppingIntervalNs       = 20_000_000_000;
    });

    // ===== MEMORY =====

    var coordinatorPrincipal : ?Principal = null;

    let memoryIndexes = Map.empty<CanistersKinds.IndexesKind, List.List<Principal>>();

    // ===== JOBS =====

    ignore Timer.setTimer<system>(
        #seconds(0),
        func () : async () {
            ignore Timer.recurringTimer<system>(#hours(24), topCanisterRequest);
            await topCanisterRequest();
        }
    );

    // ===== SYSTEM =====

    type InspectParams = {
        arg: Blob;
        caller : Principal;
        msg : {
            #systemSetCoordinator : () -> (coordinatorPrincipalArg : Principal);
            #systemAddIndex : () -> (indexPrincipal : Principal, indexKind : CanistersKinds.IndexesKind);

            #handlerGetIndexes : () -> ();
        };
    };

    system func inspect(params: InspectParams) : Bool {
        if ( params.caller == Principal.anonymous() ) { return false; };

        if ( Blob.size(params.arg) > 50 ) { return false; };

        switch ( params.msg ) {
            case (#systemSetCoordinator(_))     params.caller == owner;
            case (#systemAddIndex(_))           ?params.caller == coordinatorPrincipal or params.caller == owner;
            case (#handlerGetIndexes(_))        true
        }
    };

    public shared func systemSetCoordinator(coordinatorPrincipalArg: Principal) : async () {
        coordinatorPrincipal := ?coordinatorPrincipalArg;
    };

    public shared func systemAddIndex(indexPrincipal: Principal, indexKind: CanistersKinds.IndexesKind) : async () {
        switch ( memoryIndexes.get(CanistersKinds.compareIndexesKind, indexKind) ) {
            case (null) memoryIndexes.add(CanistersKinds.compareIndexesKind, indexKind, List.singleton(indexPrincipal));
            case (?list) list.add(indexPrincipal);    
        };
    };

    // ===== HANDLERS =====

    public query func handlerGetIndexes() : async [(CanistersKinds.IndexesKind, [Principal])] {
        let newMap = memoryIndexes.map(func(k,v) = v.toArray());
        Iter.toArray(newMap.entries())
    };
};